plugins {
    id "application"
    id "com.exactpro.th2.gradle.component" version "0.2.3"
}

group = 'com.exactpro.th2'
version = release_version

th2JavaRelease {
    targetJavaVersion.set(JavaVersion.VERSION_11)
}

ext {
    cradleVersion = '5.4.4-dev'
}

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    mavenLocal()

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

dependencies {
    implementation('com.exactpro.th2:common:5.14.0-dev') {
        exclude group: 'com.exactpro.th2', module: 'cradle-core'
        exclude group: 'com.exactpro.th2', module: 'cradle-cassandra'
    }
    implementation("com.exactpro.th2:common-utils:2.3.0-dev") {
        because("executor service utils is used")
    }
    implementation 'com.exactpro.th2:task-utils:0.1.3'
    implementation "com.exactpro.th2:cradle-core:$cradleVersion"
    implementation "com.exactpro.th2:cradle-cassandra:$cradleVersion"

    // FIXME: Added as API dependency to common com.exactpro.th2.common.event.Event.toProto(...) throws `JsonProcessingException`
    implementation 'com.fasterxml.jackson.core:jackson-core'

    implementation 'io.prometheus:simpleclient'
    implementation "org.slf4j:slf4j-api"
    implementation "org.jetbrains:annotations"

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-jsr310') {
        because("Error collector serialise Instant values")
    }

    testImplementation 'org.apache.commons:commons-lang3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.15.2'
    testRuntimeOnly('org.junit.platform:junit-platform-launcher') {
        because 'This artifact will not be added automatically since gradle 9.0. You can read more information here:' +
                'https://docs.gradle.org/8.3/userguide/upgrading_version_8.html#test_framework_implementation_dependencies'
    }
}

application {
    mainClassName 'com.exactpro.th2.estore.EventStore'
}

test {
    useJUnitPlatform()
}

dependencyCheck {
    suppressionFile='suppressions.xml'
}