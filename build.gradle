plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
    id "org.owasp.dependencycheck" version "8.1.0"
}

ext {
    sharedDir                   = file("${project.rootDir}/shared")
    cradleVersion               = '3.1.5-th2-4877-4667162750-4a37e05-SNAPSHOT'
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }

    mavenCentral()

    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
            'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
            'Specification-Title':      '',
            'Specification-Vendor':     'Exactpro Systems LLC',
            'Implementation-Title':     project.archivesBaseName,
            'Implementation-Vendor':    'Exactpro Systems LLC',
            'Implementation-Vendor-Id': 'com.exactpro',
            'Implementation-Version':   project.version
        )
    }
}

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.2.0')
    implementation 'com.exactpro.th2:common:3.44.1'
    implementation 'com.exactpro.th2:task-utils:0.1.1'
    implementation("com.exactpro.th2:cradle-core:$cradleVersion") {
        because('need to override common dep. remove in release')
        force true
    }
    implementation("com.exactpro.th2:cradle-cassandra:$cradleVersion") {
        because('need to override common dep. remove in release')
        force true
    }

    implementation 'io.prometheus:simpleclient:0.9.0'
    implementation "org.slf4j:slf4j-api:1.7.36"
    implementation "org.jetbrains:annotations"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.6.1'
}

test {
    useJUnitPlatform()
}

application {
    mainClassName 'com.exactpro.th2.estore.EventStore'
}

applicationName = 'service'

distTar {
    archiveFileName.set("${applicationName}.tar")
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

dependencyCheck {
    formats=['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS=5

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}