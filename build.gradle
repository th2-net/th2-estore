plugins {
    id 'com.google.protobuf' version '0.8.8' apply false
    id 'com.palantir.docker' version '0.25.0' apply false
    id "de.undercouch.download" version "4.0.4" apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    ext {
        sharedDir                   = file("${project.rootDir}/shared")
        junitJupiterEngineVersion   = '5.4.0'
        cradleVersion               = '2.4.0'
    }

    group = 'com.exactpro.th2'
    version = release_version

    sourceCompatibility = 11
    targetCompatibility = 11
    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/th2'
        }

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    jar {
        manifest {
            attributes(
                    'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                    'Specification-Title':      '',
                    'Specification-Vendor':     'Exactpro Systems LLC',
                    'Implementation-Title':     project.archivesBaseName,
                    'Implementation-Vendor':    'Exactpro Systems LLC',
                    'Implementation-Vendor-Id': 'com.exactpro',
                    'Implementation-Version':   project.version
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

}

apply plugin: 'application'
apply plugin: 'com.palantir.docker'

dependencies {
    api platform('com.exactpro.th2:bom:2.9.1')

    implementation "com.exactpro.th2:common:2.7.0"
    implementation "com.exactpro.th2:grpc-common:2.3.2"
    implementation "com.exactpro.th2:store-common:2.7.0"
    implementation "com.exactpro.th2:cradle-cassandra:${cradleVersion}"
    implementation "com.exactpro.th2:cradle-core:${cradleVersion}"

    implementation 'com.google.protobuf:protobuf-java-util'

    implementation "org.jetbrains:annotations:20.0.0"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

application {
    mainClassName 'com.exactpro.th2.estore.EventStoreMain'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}
